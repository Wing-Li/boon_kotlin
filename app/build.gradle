apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

//加载资源.在本地的 local.properties 文件设置了一些参数，从这里获取
Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
properties.load(inputStream)

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.3'

    // 签名文件
    signingConfigs {
        config {
            keyAlias 'lyl'
            keyPassword '123456'
            storeFile file('../lyl.jks')
            storePassword '123456'
        }
    }

    defaultConfig {
        applicationId "com.lyl.boon.kotlin"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 4
        versionName "1.1.0"
        ndk {
            abiFilters 'armeabi' //设置支持的SO库架构, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }

        flavorDimensions "app"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    // 多渠道/多环境 的不同配置
    productFlavors {
        dev {
            dimension "app"

            // 每个环境包名不同
            applicationId "com.lyl.boon.dev.kotlin"
            // 动态添加 string.xml 字段；
            // 注意，这里是添加，在 string.xml 不能有这个字段，会重名！！！
            resValue "string", "app_name", "dev_boon_kotlin"
            resValue "bool", "isrRank", 'false'
            // 动态修改 常量 字段
            buildConfigField "String", "ENVIRONMENT", '"dev"'
            buildConfigField "String", "BUGLYAPPID", properties.getProperty('appIdMark')
            // 修改 AndroidManifest.xml 里渠道变量
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "dev",
                                    app_icon   : "@drawable/logo"]
        }
        stage {
            dimension "app"

            applicationId "com.lyl.boon.stage"

            resValue "string", "app_name", "stage_boon"
            resValue "bool", "isrRank", 'true'

            buildConfigField "String", "ENVIRONMENT", '"stage"'
            buildConfigField "String", "BUGLYAPPID", properties.getProperty('appIdMark')

            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "stage",
                                    app_icon   : "@drawable/logo"]
        }
        prod {
            dimension "app"

            applicationId "com.lyl.boon"

            resValue "string", "app_name", "boon"
            resValue "bool", "isrRank", 'true'

            buildConfigField "String", "ENVIRONMENT", '"prod"'
            buildConfigField "String", "BUGLYAPPID", properties.getProperty('appIdMark')

            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "prod",
                                    app_icon   : "@drawable/logo"]
        }
    }

    //移除lint检测的error
    lintOptions {
        abortOnError false
    }

    dexOptions {
        preDexLibraries = false
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }

        release {
            minifyEnabled false
            zipAlignEnabled true
            //移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            // 批量打包
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${releaseTime()}.apk"
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    compile 'com.github.bumptech.glide:glide:4.3.1'
    compile 'com.commit451:PhotoView:1.2.5'

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'

    compile 'io.reactivex:rxjava:1.2.3'
    compile 'io.reactivex:rxandroid:1.2.1'

    compile 'com.roughike:bottom-bar:2.3.1'

    //一个不错的BaseAapter封装类
    compile 'org.byteam.superadapter:superadapter:3.3'

    //Material Dialog
    compile 'me.drakeet.materialdialog:library:1.3.1'

    //非常不错的 loading 加载库
    compile 'com.wang.avi:library:1.0.5'
    compile 'com.nineoldandroids:library:2.4.0'
    //应用升级
    compile 'com.tencent.bugly:crashreport_upgrade:1.3.4'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
